apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'eclipse'

mainClassName = "io.andrewohara.tinkertime.launcher.TinkerTimeLauncher"

repositories {
    mavenCentral()
}

sourceSets {

    main {
        java {
            srcDirs ('src', 'Common/src')
            exclude ('src/aohara/tinkertime/CountDownloads.java')
        }
        resources {
            srcDir 'res'
            exclude('*/Thumbs.db')
        }
    }

    test {
        java {
            srcDir 'test'
        }
        resources {
            srcDir 'testRes'
        }
    }
}

compileJava {
  sourceCompatibility = '1.7'
  targetCompatibility = '1.7'
}

dependencies {

	compile 'org.flywaydb:flyway-core:3.2.1'
	compile 'com.j256.ormlite:ormlite-jdbc:4.48'
	compile 'com.j256.ormlite:ormlite-core:4.48'
	compile 'org.xerial:sqlite-jdbc:3.8.10.1'
	compile 'com.google.inject:guice:4.0'
	compile 'commons-io:commons-io:2.4'
	compile 'com.google.code.gson:gson:2.3'
	compile 'org.jsoup:jsoup:1.7.3'

    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile 'org.mockito:mockito-all:1.9.5'
    testCompile 'junit:junit:4.11'
}

jar {

    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }

    manifest {
        attributes 'Main-Class': mainClassName
    }
}

task release(type:Zip,dependsOn:jar) {
	jar.outputs.files.each {
		from it.getPath()
	}
	from "readme.md"
}
